apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

// generic method io logger
apply plugin: 'com.jakewharton.hugo'

// ReactiveX observable logger
apply plugin: 'com.fernandocejas.frodo'

android {
    compileSdkVersion = rootProject.ext.compileSdkVersion
    buildToolsVersion = rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "yuki.android.ormasample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    productFlavors {
        // Define separate dev and prod product flavors.
        // http://developer.android.com/intl/ja/tools/building/multidex.html#dev-build
        dev {
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            minSdkVersion 21
        }
        pro {
            // The actual minSdkVersion for the application.
            minSdkVersion rootProject.ext.minSdkVersion
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    
    // ビルド高速化
    // DEXプロセスを分離プロセスではなく, 同Gradleプロセス上で実施することでビルドを高速化する.
    // 副作用としてJVMのメモリを大量に消費するため, メモリ確保ができない場合はこれをコメントアウトすること. 
    // http://stackoverflow.com/questions/35224255/to-run-dex-in-process-the-gradle-daemon-needs-a-larger-heap-it-currently-has-9
    dexOptions {
        // DexOptions API: http://google.github.io/android-gradle-dsl/current/com.android.build.gradle.internal.dsl.DexOptions.html
        javaMaxHeapSize "4g"
        dexInProcess = true
    }

    // ライブラリ間の依存性解決 
    // https://sites.google.com/a/android.com/tools/tech-docs/new-build-system/user-guide#TOC-Resolving-conflicts-between-main-and-test-APK
    // 
    // configurations.allはcode inspectionが報告されるため下記のコメントをつける. 
    // see https://youtrack.jetbrains.com/issue/IDEA-141744
    // noinspection GroovyAssignabilityCheck
    configurations.all {
        resolutionStrategy.force "com.android.support:support-annotations:${supportPackageVersion}"
    }
}

//noinspection GroovyAssignabilityCheck
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Support lib.
    compile "com.android.support:appcompat-v7:${supportPackageVersion}"
    compile "com.android.support:design:${supportPackageVersion}"
    compile "com.android.support:support-annotations:${supportPackageVersion}"
    
    // apt.
    provided 'javax.annotation:javax.annotation-api:1.2'

    // ReactiveX.
    compile 'io.reactivex:rxandroid:1.1.0'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.0'

    // Logger
    compile 'com.jakewharton.timber:timber:4.1.0'

    // DI. Dagger2
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    compile 'com.google.dagger:dagger:2.0.2'

    // View bind. Butterknife
    compile 'com.jakewharton:butterknife:7.0.1'

    // ORM. Orma
    apt 'com.github.gfx.android.orma:orma-processor:1.1.2'
    compile 'com.github.gfx.android.orma:orma:1.1.2'

    // JUnit
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    // Unit testing. Robolectric
    testCompile "org.robolectric:robolectric:3.0"
    testCompile 'junit:junit:4.12'

    // android Testing support lib.
    androidTestCompile "com.android.support.test:runner:${testingSupportPackageVersion}"
    androidTestCompile "com.android.support.test:rules:${testingSupportPackageVersion}"
}

// メソッドのIOをロギングするHugoライブラリをdebugビルドでもOFFにしたい場合は
// 下記のコメントアウトを外す. https://github.com/JakeWharton/hugo
//hugo {
//    enabled false
//}